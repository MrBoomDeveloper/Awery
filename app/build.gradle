import groovy.json.JsonSlurper

plugins {
    id "com.android.application"
    id "kotlin-android"
}

android {
    namespace "com.mrboomdev.awery"
    compileSdk 34

    defaultConfig {
        applicationId "com.mrboomdev.awery"
        targetSdk 34
        versionName "1.0.5"

        /* We want to make sure that all build versions across the planet will be in sync */
        var timezone = TimeZone.getTimeZone("UTC")
        var calendarCurrent = Calendar.getInstance(timezone)

        /* Just a random date to begin with */
        var calendarBegin = Calendar.getInstance(timezone)
        calendarBegin.set(2024, 2, 4)

        /* Only one version can be published per hour or else horrible things will happen */
        var newVersionCodeTimeout = 60 * 60 * 1000
        versionCode (((calendarCurrent.timeInMillis - calendarBegin.timeInMillis) / newVersionCodeTimeout) as int)

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    androidResources {
        generateLocaleConfig true
    }

    sourceSets {
        main {
            assets.srcDirs += files("$projectDir/schemas".toString())
            java.srcDirs += file("$projectDir/awery_gen/main/java")
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            versionNameSuffix "-debug"
        }

        release {
            versionNameSuffix "-release"
            debuggable false
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.debug
        }
    }

    flavorDimensions = [ "type", "sdk" ]

    productFlavors {
        alpha {
            dimension "type"
            versionNameSuffix "-alpha"
            applicationIdSuffix ".alpha"
            buildConfigField("boolean", "IS_BETA", "true")
            buildConfigField("String", "CHANNEL", "\"ALPHA\"")
            manifestPlaceholders.appLabel = "Awery Alpha"

            buildConfigField("String", "FILE_PROVIDER", "\"com.mrboomdev.awery.alpha.FileProvider\"")
            manifestPlaceholders.fileProvider = "com.mrboomdev.awery.alpha.FileProvider"
        }

        beta {
            isDefault true

            dimension "type"
            versionNameSuffix "-beta"
            applicationIdSuffix ".beta"
            buildConfigField("boolean", "IS_BETA", "true")
            buildConfigField("String", "CHANNEL", "\"BETA\"")
            manifestPlaceholders.appLabel = "Awery Beta"

            buildConfigField("String", "FILE_PROVIDER", "\"com.mrboomdev.awery.beta.FileProvider\"")
            manifestPlaceholders.fileProvider = "com.mrboomdev.awery.beta.FileProvider"
        }

        prod {
            dimension "type"
            versionNameSuffix "-stable"
            buildConfigField("boolean", "IS_BETA", "false")
            buildConfigField("String", "CHANNEL", "\"STABLE\"")
            manifestPlaceholders.appLabel = "Awery"

            buildConfigField("String", "FILE_PROVIDER", "\"com.mrboomdev.awery.FileProvider\"")
            manifestPlaceholders.fileProvider = "com.mrboomdev.awery.FileProvider"
        }

        modern {
            versionNameSuffix "-modern"
            dimension "sdk"
            buildConfigField("boolean", "IS_LEGACY", "false")
            minSdk 25
            isDefault true
        }

        legacy {
            versionNameSuffix "-legacy"
            dimension "sdk"
            buildConfigField("boolean", "IS_LEGACY", "true")
            minSdk 23
        }
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
        freeCompilerArgs = ["-Xcontext-receivers", "-Xmulti-platform"]
    }
}

dependencies {
    // AwerySdk
    implementation "com.github.MrBoomDeveloper:AwerySdk:67856bbc66"

    // Compatibility for new methods in old Api
    implementation "androidx.core:core-ktx:1.13.1"
    implementation "androidx.work:work-runtime-ktx:2.9.0"
    implementation "androidx.appcompat:appcompat:1.7.0"
    implementation "androidx.browser:browser:1.8.0"
    implementation "androidx.webkit:webkit:1.11.0"
    implementation "androidx.fragment:fragment-ktx:1.7.1"
    implementation "androidx.preference:preference-ktx:1.2.1"

    // Exoplayer
    ext.exo_version = "1.3.1"
    implementation "androidx.media3:media3-exoplayer:$exo_version"
    implementation "androidx.media3:media3-ui:$exo_version"
    implementation "androidx.media3:media3-exoplayer-hls:$exo_version"
    implementation "androidx.media3:media3-exoplayer-dash:$exo_version"
    implementation "androidx.media3:media3-datasource-okhttp:$exo_version"
    implementation "androidx.media3:media3-session:$exo_version"

    // Database
    def room_version = "2.6.1"
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"

    // UI
    implementation "androidx.recyclerview:recyclerview:1.3.2"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation "com.google.android.material:material:1.12.0"
    implementation "nl.joery.animatedbottombar:library:1.1.0"
    implementation "com.davemorrissey.labs:subsampling-scale-image-view-androidx:3.10.0"
    implementation "com.github.VipulOG:ebook-reader:0.1.6"
    implementation "com.github.emilaleborn:PageIndicatorView:905cd4e2ab"
    implementation "androidx.paging:paging-runtime-ktx:3.3.0"
    implementation "com.github.skydoves:colorpickerview:2.3.0"
    implementation "androidx.core:core-splashscreen:1.0.1"
    implementation "com.github.skydoves:balloon:1.6.4"
    implementation "nl.dionsegijn:konfetti-xml:2.0.4"
    implementation "com.rubensousa.dpadrecyclerview:dpadrecyclerview:1.3.0-alpha04"

    // Markdown
    for(String module_name : List.of("core", "editor", "ext-strikethrough", "html", "image", "image-glide", "linkify")) {
        implementation "io.noties.markwon:$module_name:4.6.2"
    }

    // Utils
    implementation "me.xdrop:fuzzywuzzy:1.4.0"
    implementation "com.squareup.moshi:moshi:1.15.0"
    implementation "org.mozilla:rhino:1.7.14"
    implementation "com.iqiyi.xcrash:xcrash-android-lib:3.1.0"
    implementation "net.sourceforge.streamsupport:android-retrostreams:1.7.4"

    // Networking
    ext.glide_version = "4.16.0"
    ext.okhttp_version = "5.0.0-alpha.12"
    api "com.github.bumptech.glide:glide:$glide_version"
    implementation "com.github.bumptech.glide:annotations:glide_version"
    annotationProcessor "com.github.bumptech.glide:compiler:4.14.2"
    implementation "com.github.bumptech.glide:glide:$glide_version"
    implementation "com.github.bumptech.glide:okhttp3-integration:$glide_version"
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    implementation "com.squareup.okhttp3:okhttp-dnsoverhttps:$okhttp_version"
    implementation "com.squareup.okhttp3:okhttp-brotli:4.12.0"

    // Aniyomi
    ext.kotlin_serialization_verison = "1.6.2"
    implementation "app.cash.quickjs:quickjs-android:0.9.2"
    implementation "io.reactivex:rxjava:1.3.8"
    implementation "io.reactivex:rxandroid:1.2.1"
    implementation "com.github.inorichi.injekt:injekt-core:65b0440"
    implementation "org.jsoup:jsoup:1.15.4"
    implementation "com.github.gpanther:java-nat-sort:natural-comparator-1.1"
    implementation "com.github.tachiyomiorg:unifile:17bec43"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$kotlin_serialization_verison"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json-okio:$kotlin_serialization_verison"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-protobuf:$kotlin_serialization_verison"
}

static def writeFile(File file, String content) {
    try(var writer = new BufferedWriter(new FileWriter(file))) {
        writer.write(content)
    } catch(IOException e) {
        throw new IllegalStateException("Failed to read an file! " + file, e)
    }
}

static def readFile(File file) {
    try(var reader = new BufferedReader(new FileReader(file))) {
        var builder = new StringBuilder()
        String line

        while((line = reader.readLine()) != null) {
            builder.append(line)
        }

        return builder.toString()
    } catch(IOException e) {
        throw new IllegalStateException("Failed to read an file! " + file, e)
    }
}

static def toCamelCase(String text) {
    var result = ""

    for(String s : text.split("_")) {
        result += Character.toUpperCase(s.charAt(0))

        if(s.length() > 1) {
            result += s.substring(1, s.length()).toLowerCase()
        }
    }

    return result
}

static def formatKey(Object o) {
    var result = "    public static final "

    if(o.type == "select" && o.items != null) {
        var enumName = toCamelCase(o.key.toUpperCase()) + "_Values"

        result = "\n" + result
        result += "EnumSetting<$enumName> ${o.key.toUpperCase()} = new EnumSetting<>(\"${o.key}\", ${enumName}.class);\n\n"

        result += "    public enum ${enumName} implements EnumWithKey {\n        "

        var iterator = o.items.iterator()

        while(iterator.hasNext()) {
            var item = iterator.next()
            result += item.key.toUpperCase() + "(\"${item.key}\")"

            if(iterator.hasNext()) {
                result += ", "
            }
        }

        result += ";\n\n        private final String key;\n\n        ${enumName}(String key) {\n"
        result += "            this.key = key;\n        }\n\n        @Override\n        public String getKey() {\n"
        result += "            return key;\n        }\n    }\n\n"

        return result
    }

    if(List.of("action", "select", "multiselect").contains(o.type)) {
        return result + "String ${o.key.toUpperCase()} = \"${o.key}\";\n"
    }

    switch(o.type) {
        case "string": result += "String"; break
        case [ "integer", "select_integer" ]: result += "Integer"; break
        case "boolean": result += "Boolean"; break
    }

    result += "Setting ${o.key.toUpperCase()} = () -> \"${o.key}\";\n"
    return result
}

static def collectKeys(Object from) {
    var builder = new StringBuilder()

    switch(from.type) {
        case "screen": {
            for(Object item : from.items) {
                builder.append(collectKeys(item))
            }
        }; break

        case [ "string", "integer", "boolean", "action",
               "select", "select_integer", "multiselect"
        ] : builder.append(formatKey(from)); break
    }

    return builder.toString()
}

tasks.register("generateClasses") {
    def jsonSlurper = new JsonSlurper()
    def settings = jsonSlurper.parseText(readFile(file("$projectDir/src/main/assets/settings.json")))

    def dir = file("$projectDir/awery_gen/main/java/com/mrboomdev/awery/generated")
    dir.mkdirs()

    var builder = new StringBuilder()

    builder.append("package com.mrboomdev.awery.generated;\n" +
            "\n" +
            "import com.mrboomdev.awery.data.settings.NicePreferences.*;\n" +
            "\n" +
            "/**\n" +
            " * Auto-generated class created during the compilation. Please, do not edit it.\n" +
            " * @author MrBoomDev\n" +
            " */\n" +
            "public class AwerySettings {\n")

    builder.append(collectKeys(settings))
    builder.append("}")

    writeFile(new File(dir, "AwerySettings.java"), builder.toString())
}

preBuild.dependsOn generateClasses